// Taken from Alex's talk
// https://github.com/wyvernlang/wyvern/blob/master/examples/AlexTalkDemos/08-SealerUnsealer/SealerUnsealer.wyv

require stdout

import wyvern.option
import wyvern.collections.map
import wyvern.collections.list
import wyvern.String

// This example would be a lot nicer if parameterised Option[T]
// worked! Will rewrite when this bug is fixed. :-)
type Option = option.Option

type Map = map.Map

type List = list.List

type Box
     type T
     val name:String

resource type Sealer
     def seal(object:Option):Box[object.T]

resource type Unsealer
     def unseal(box:Box):Option[box.T]

resource type SealerUnsealer
     var sealer:Sealer
     var unsealer:Unsealer

def makeSealerUnsealer():SealerUnsealer
    var map:Map = map.make((b1:Box,b2:Box) => (b1.name.equals(b2.name)))
    var currentKey:String = "S"
    new
        var sealer:Sealer = new
            def seal(object:Option):Box[object.T]
                val newBox = new
                    type T = object.T
                    val name:String = currentKey + "S" // This needs to be made more secure than this!!!
                map.put(newBox,object)
                newBox
        var unsealer:Unsealer = new
            def unseal(box:Box):Option[box.T]
                map.get(box)


/*
# http://wiki.erights.org/wiki/Walnut/Secure_Distributed_Computing/Capability_Patterns#Sealers_and_Unsealers
# E version that may have concurrency issues due to using "shared" variable in non-E languages:
*/

resource type SealedBox
    type T
    def shareContent():Unit

resource type BrandSealer
    type T
    def seal(object:Option[this.T]):SealedBox[this.T]

resource type BrandUnsealer
    type T
    def unseal(box:SealedBox[this.T]):Option[this.T]

resource type BrandPair
    type T
    var name:String
    var sealer:BrandSealer[this.T]
    var unsealer:BrandUnsealer[this.T]

def makeBrandPair[T](name:String):BrandPair[T]
    var shared:Option[T] = option.None[T]()
    def makeSealedBox(object:Option[T]):SealedBox[T]
        val newBox:SealedBox[T] = new
            type T = T
            def shareContent():Unit
                shared = object
        newBox
    new
        type T = T
        var name:String = name
        var sealer:BrandSealer[T] = new
            type T = T
            def seal(object:Option[T]):SealedBox[T]
                makeSealedBox(object)
        var unsealer:BrandUnsealer[T] = new
            type T = T
            def unseal(box:SealedBox[T]):Option[T]
                shared = option.None[T]()
                box.shareContent()
                var result:Option[T] = shared
                result


stdout.print("\nMINT EXAMPLE:\n")

resource type Mint
     def makePurse(balance:Int):Purse
     def print():Unit

resource type Purse
     def getBalance():Int
     def sprout():Purse
     def getDecr():SealedBox[Int -> Unit]
     def deposit(amount:Int, src:Purse):Unit
     def print():Unit

def makeMint(name:String):Mint
    var brandPair:BrandPair[Int -> Unit] = makeBrandPair[Int -> Unit](name)
    val blankBox:SealedBox[Int -> Unit] = new
        type T = Int -> Unit
        def shareContent():Unit
            stdout.print("This is a blank box.")
    new (selfMint) =>
        def makePurse(balance:Int):Purse
            var balance:Int = balance
            val decr:Int -> Unit = ((amount:Int) => (balance = balance - amount))
            new (selfPurse) =>
                def getBalance():Int = balance
                def sprout():Purse = selfMint.makePurse(0)
                def getDecr():SealedBox[Int -> Unit] = brandPair.sealer.seal(option.Some(decr))
                def deposit(amount:Int, src:Purse):Unit
                    var option:Option[Int -> Unit] = brandPair.unsealer.unseal(src.getDecr())
                    option.getOrElse(() => (a:Int) => stdout.print("Blank box detected"))(amount)
                    balance = balance + amount
                def print():Unit
                    stdout.print("Purse that has ")
                    stdout.printInt(balance)
                    stdout.print(" bucks from mint named " + brandPair.name + "\n")
        def print():Unit
            stdout.print("Mint named " + brandPair.name + "\n")


var carolMint:Mint = makeMint("Carol")
carolMint.print()

var aliceMainPurse:Purse = carolMint.makePurse(1000)
aliceMainPurse.print()
stdout.print("This should be 1000:\n")
stdout.printInt(aliceMainPurse.getBalance())
stdout.print("\n")

var bobMainPurse:Purse = carolMint.makePurse(0)
bobMainPurse.print()

var paymentForBob:Purse = aliceMainPurse.sprout()
paymentForBob.print()

paymentForBob.deposit(10, aliceMainPurse)
paymentForBob.print()

bobMainPurse.deposit(10, paymentForBob)
stdout.print("Should be 10:\n")
bobMainPurse.print()
stdout.print("Should be 990:\n")
aliceMainPurse.print()
